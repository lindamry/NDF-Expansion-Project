NDFENTER ;BIR/DMA-enter/edit options for NDF ; 08 Sep 2008  11:01 AM
 ;;4.0; NDF MANAGEMENT;; 1 Jan 99
 ;
P ;ENTER/EDIT PRODUCT
 D ADDONE^PRODUCT Q
 ;
1 ;VA GENERIC
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.6 W !! D ^DIC Q:Y<0  S DR=".01;1;",DA=+Y D ^DIE
 K DIC,DIE,DR Q
 ;
2 ;VA CLASS
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.605 W !! D ^DIC Q:Y<0  S DR=".01;1;2;3;4;",DA=+Y D ^DIE S Z="PS(50.605,"_DA_",1)" I '$D(^NDFK(5000,1,4,"B",Z)) S X=$O(^NDFK(5000,1,4," "),-1)+1,^(X,0)=Z,^NDFK(5000,1,4,"B",Z,X)=""
 K DIC,DIE,DR Q
 ;
3 ;MANFACTUERER
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=55.95 W !! D ^DIC Q:Y<0  S DR=".01;1;2;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
4 ;DOSE FORM
 ; NEW fields added to template 1/17/2002 and 9/8/2008
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.606,NDFOK=1 W !! D ^DIC Q:Y<0  S DR=".01;7;9;8;11;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR,NDFOK Q
 ;
5 ;UNITS
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.607 W !! D ^DIC Q:Y<0  S DR=".01;1;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
6 ;PACKAGE TYPE
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.608 W !! D ^DIC Q:Y<0  S DR=".01;1;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
7 ;PACKAGE SIZE
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.609 W !! D ^DIC Q:Y<0  S DR=".01;1;",DA=+Y D ^DIE
 K DIC,DIC,DA,DR Q
 ;
8 ;VA UNITS
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.64 W !! D ^DIC Q:Y<0  S DR=".01;1;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
9 ;ROUTE
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=51.2 W !! D ^DIC Q:Y<0  S DR=".01;1;5;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
10 ;INGREDIENTS
 F  S DIC(0)="AEQLMZ",(DIC,DIE,DLAYGO)=50.416 W !! D ^DIC Q:Y<0  S DR=".01;2;3;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
11 ;INTERACTIONS
 D ^NDFDINT
 Q
 ;
12 ;FORMULARY INFORMATION
 F  S DIC(0)="AEQMZ",(DIC,DIE,DLAYGO)=50.68 W !! D ^DIC Q:Y<0  S DR="17;18;",DA=+Y D ^DIE
 K DIC,DIE,DA,DR Q
 ;
14 ;CS FEDERAL SCHEDULE
 K ^TMP($J)
 K DIR S DIR(0)="FO",DIR("A")="Select VA PRODUCT NAME " D ^DIR I $D(DIRUT) K DIR,DIRUT,X,Y Q
 K DIR D FIND^DIC(50.68,,,"M",Y,,,,,"^TMP($J)")
 ;D LIST^DIC(50.68,,,"",,,Y,,,,"^TMP($J)") K DIR
 S J=0 F  S J=$O(^TMP($J,"DILIST",1,J)) Q:'J  S DA=^TMP($J,"DILIST",2,J),NA=^TMP($J,"DILIST",1,J) W !,NA,! S DIR(0)="50.68,19Or" D ^DIR Q:$D(DTOUT)  Q:$D(DUOUT)  D
 .S OLD=$P($G(^PSNDF(50.68,DA,7)),"^"),NEW=$P(Y,"^"),DIE=50.68,DR="19////"_$P(Y,"^") D ^DIE I OLD'=NEW S ^NDFK(5000.9,DA,0)=DA
 K DA,DIE,DIR,J,NA,X,Y,^TMP($J) Q
 ;
PFS ;SERVICE CODE
 F  S DIC=50.68,DIC(0)="AEQM" D ^DIC Q:Y<0  S DIE=DIC,DA=+Y,DR="2000;" D ^DIE
 K DIC,DIE,Y Q
 ;
