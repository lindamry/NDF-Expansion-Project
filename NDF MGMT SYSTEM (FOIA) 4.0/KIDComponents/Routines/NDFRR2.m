NDFRR2 ;BIR/RSB-PRODUCT EDIT ; 21 Aug 2009  6:45 AM
 ;;4.0; NDF MANAGEMENT;**62**; 1 Jan 99
 ;
 W @IOF K ^TMP("NDFR",$J) D FRESH^NDFRR10 S NDFNEW=1
1 ; get VA Product Name
 D SHOW^NDFRR998
 I $G(^TMP("NDFD",$J,"FDBLINK")) W !!,"Associated FDB Information:",! W $P($G(^TMP("NDFD",$J,"FDBLINK")),"^"),!!
 K NDFNAME,NDFNW,^TMP("NDFR",$J)
 S (NDFM,X)=$$GET^NDFRR1(50.68,.01,$G(NDFVAPD),0,"Enter VA Product Name")
 I NDFM=""!(NDFM="^") Q
 D FRESH^NDFRR10
 K DIC S DIC=50.68,DIC(0)="EMZ",DIC("W")="I Y>0&($G(^PSNDF(50.68,+Y,7))'="""") I $P(^(7),U,3)'="""" S Y1=Y,Y=$P(^(7),U,3) D DD^%DT W Y S Y=Y1" D ^DIC
 ;S DIC=50.68,DIC(0)="EMZ" D ^DIC
 I +Y>0 S NDFNAME=Y S:$D(NDFNDC)!($D(NDFUPN))!($D(NDFUPNI)) ^TMP("NDFD",$J,"50.67,5")=+Y K NDFCLASS,NDFINGL D EN^VALM("NDF RSB ACCEPT/EDIT") Q:$D(NDFNDC)!($D(NDFUPN))!($D(NDFUPNI))  G 1
 I Y=-1 D
 .I '$$CK^NDFRR1(50.68,.01,NDFM) D HLP^NDFRR1(50.68,.01) G 1
 .S NDFADD=0 I $$ADD^NDFRR1(NDFM,"VA PRODUCT") K ^TMP("NDFR",$J) D SET^NDFRR1("50.68,.01",NDFM) S:$L(^TMP("NDFR",$J,"50.68,.01"))<41 ^TMP("NDFR",$J,"50.68,5")=^TMP("NDFR",$J,"50.68,.01") S NDFADD=1
 I '$G(NDFADD) G 1
 S NDFNW=1 ; is this a new entry
 ;
2 ;  ACTIVE INGREDIENTS
 S CNT=0
21 W ! S (NDFM,X)=$$GET^NDFRR1(50.416,.01,"",0,"Active Ingredients")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I NDFM=""&('$D(NDFEDIT)) G 5
 I NDFM=""&($D(NDFEDIT)) Q
 K DIC S DIC=50.416,DIC(0)="EMZ" D ^DIC
 S NDFY=Y I NDFY=-1 D
 .S CNT=CNT+1
 .I '$$CK^NDFRR1(50.416,.01,NDFM) D HLP^NDFRR1(50.416,.01) Q
 .S NDFADD=0 I $$ADD^NDFRR1(NDFM,"DRUG INGREDIENT") S ^TMP("NDFR",$J,"50.6814",CNT)=NDFM,NDFADD=1 D
 ..K DIR S DIR(0)="Y",DIR("A")="Is this a primary ingredient" D ^DIR
 ..I Y=0 D
 ...S DIC=50.416,DIC(0)="QEALMZ",DIC("A")="Enter Primary ingredient: " D ^DIC
 ...I Y<0 W !,"Not Marked"
 ...I Y>0 S $P(^TMP("NDFR",$J,"50.6814",CNT),"|",4)=+Y
 I NDFY=-1&('NDFADD) G 21
 I NDFY>0 D
 .I $P(^PS(50.416,+Y,0),"^",2) W !?30," (Primary Ingredient)",$C(7)
 .S CNT=CNT+1 S ^TMP("NDFR",$J,"50.6814",CNT)=+Y
 Q:$D(NDFEDIT)
 ;
 ;
3 ;  STRENGTH
 S (NDFM,X)=$$GET^NDFRR1(50.6814,1,"",0,"Strength")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM=""&('$D(NDFEDIT)) G 4
 I NDFM=""&($D(NDFEDIT)) Q
 I '$$CK^NDFRR1(50.6814,1,NDFM) D HLP^NDFRR1(50.6814,1) G 3
 I $$CK^NDFRR1(50.6814,1,NDFM) S $P(^TMP("NDFR",$J,"50.6814",CNT),"|",2)=NDFM
 Q:$D(NDFEDIT)
 ;
4 ;  UNITS
 S (NDFM,X)=$$GET^NDFRR1(50.6814,2,"",0,"Units")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM=""&('$D(NDFEDIT)) G 21
 I NDFM=""&($D(NDFEDIT)) Q
 ;I $D(NDFABORT) G EXIT^NDFRR2A
 ;I NDFM="" Q
 K DIC S DIC=50.607,DIC(0)="EMZ" D ^DIC K DIC
 I Y=-1 D  G 4
 .I '$$CK^NDFRR1(50.6814,2,NDFM) D HLP^NDFRR1(50.6814,2)
 .;I $$CK^NDFRR1(50.6814,2,NDFM) S $P(^TMP("NDFR",$J,"50.6814",CNT),"|",3)=NDFM
 E  S NDFM=$S(Y["^":+Y,1:Y) S $P(^TMP("NDFR",$J,"50.6814",CNT),"|",3)=NDFM
 G 21
 ;
5 ; VA GENERIC NAME
 N ADD S NDFC=$G(^TMP("NDFR",$J,"50.68,.05")),NDFCC=(NDFC?1.9N)
 S (NDFM,X)=$$GET^NDFRR1(50.6,.01,$S(NDFCC:$S($D(^TMP("NDFR",$J,"50.68,.05")):$P(^PSNDF(50.6,^TMP("NDFR",$J,"50.68,.05"),0),"^"),1:""),1:NDFC),1,"VA Generic Name") K NDFC,NDFCC
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM="" Q
 K DIC S DIC=50.6,DIC(0)="EMZ" D ^DIC K DIC
 I Y=-1 I '$$CK^NDFRR1(50.6,.01,NDFM) D HLP^NDFRR1(50.68,.05) G 5
 I Y=-1 S ADD=$$ADD^NDFRR1(NDFM,"VA GENERIC NAME") I ADD S ^TMP("NDFR",$J,"50.68,.05")=NDFM
 I $G(ADD)=0 G 5
 ; screen out inactive entries
 I +Y>0 I $P(^PSNDF(50.6,+Y,0),"^",2)&($P(^PSNDF(50.6,+Y,0),"^",2)'>DT) W "    (Inactive)" H .4 G 5
 I +Y>0 S NDFM=$S(Y["^":+Y,1:Y) S ^TMP("NDFR",$J,"50.68,.05")=NDFM
 Q:$D(NDFEDIT)
 ;
 ;
6 ; DOSAGE FORM
 N NDFY
 S NDFC=$G(^TMP("NDFR",$J,"50.68,1")),NDFCC=(NDFC?1.9N)
 S (NDFM,X)=$$GET^NDFRR1(50.606,.01,$S(NDFCC:$S($D(^TMP("NDFR",$J,"50.68,1")):$P(^PS(50.606,^TMP("NDFR",$J,"50.68,1"),0),"^"),1:""),1:NDFC),1,"Dosage Form") K NDFC,NDFCC
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM="" Q
 K DIC S DIC=50.606,DIC(0)="EMZ" D ^DIC
 I Y=-1 I '$$CK^NDFRR1(50.606,.01,NDFM) D HLP^NDFRR1(50.606,1) W "  ??" G 6
 I Y=-1 S NDFY=$$ADD^NDFRR1(NDFM,"DOSAGE FORM") I NDFY S ^TMP("NDFR",$J,"50.68,1")=NDFM
 I $G(NDFY)=0 G 6
 I +Y>0 S NDFM=$S(Y["^":+Y,1:Y) S ^TMP("NDFR",$J,"50.68,1")=NDFM
 Q:$D(NDFEDIT)
 ;
 ;
7 ;  STRENGTH
 S (NDFM,X)=$$GET^NDFRR1(50.6814,1,$S($D(^TMP("NDFR",$J,"50.68,2")):^TMP("NDFR",$J,"50.68,2"),1:""),0,"Strength")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM=""&('$D(NDFEDIT)) G 8
 I NDFM=""&($D(NDFEDIT)) Q
 I '$$CK^NDFRR1(50.68,2,NDFM) D HLP^NDFRR1(50.68,2) G 7
 I $$CK^NDFRR1(50.68,2,NDFM) S ^TMP("NDFR",$J,"50.68,2")=NDFM
 Q:$D(NDFEDIT)
 ;
8 ;  UNITS
 S (NDFM,X)=$$GET^NDFRR1(50.68,3,$S($G(^TMP("NDFR",$J,"50.68,3")):$P(^PS(50.607,^TMP("NDFR",$J,"50.68,3"),0),"^"),1:""),0,"Units")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM=""&('$D(NDFEDIT)) G 9
 I NDFM=""&($D(NDFEDIT)) Q
 K DIC S DIC=50.607,DIC(0)="EMZ" D ^DIC
 I Y=-1 D  G 8
 .I '$$CK^NDFRR1(50.68,3,NDFM) D HLP^NDFRR1(50.68,3)
 .;I $$CK^NDFRR1(50.68,3,NDFM) D SET^NDFRR1("50.68,3",NDFM)
 I $$CK^NDFRR1(50.68,3,NDFM) S NDFM=$S(Y["^":+Y,1:Y) S ^TMP("NDFR",$J,"50.68,3")=NDFM
 Q:$D(NDFEDIT)
 ;
9 ; NATIONAL FORMULARY NAME
 ;
 K NDFDEF,NDFC,NDFCC
 S NDFC=$G(^TMP("NDFR",$J,"50.68,.05")) S NDFCC=(NDFC?1.9N)
 S NDFDEF=$S(NDFCC:$P($G(^PSNDF(50.6,^TMP("NDFR",$J,"50.68,.05"),0)),"^"),1:$G(^TMP("NDFR",$J,"50.68,.05")))
 S NDFC=$G(^TMP("NDFR",$J,"50.68,1")) S NDFCC=(NDFC?1.9N)
 S NDFDEF=NDFDEF_" "_$S(NDFCC:$P($G(^PS(50.606,^TMP("NDFR",$J,"50.68,1"),0)),"^"),1:^TMP("NDFR",$J,"50.68,1"))
 I $G(^TMP("NDFR",$J,"50.68,4"))]"" S NDFDEF=^("50.68,4")
 S (NDFM,X)=$$GET^NDFRR1(50.68,4,NDFDEF,1,"National Formulary Name")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM="" Q
 I '$$CK^NDFRR1(50.68,4,NDFM) D HLP^NDFRR1(50.68,4) G 9
 S ^TMP("NDFR",$J,"50.68,4")=NDFM
 S ^TMP("NDFR",$J,"50.68,4")=NDFM
 K NDFDEF
 Q:$D(NDFEDIT)
 ;
 ;
10 ; VA PRINT NAME
 ;
 K NDFDEF
 S NDFDEF=$S($L($G(^TMP("NDFR",$J,"50.68,5"))):$G(^TMP("NDFR",$J,"50.68,5")),$L(^TMP("NDFR",$J,"50.68,.01"))<41:^TMP("NDFR",$J,"50.68,.01"),1:"")
 N CHANGE S CHANGE=$G(^TMP("NDFR",$J,"50.68,5"))
 S (NDFM,X)=$$GET^NDFRR1(50.68,5,NDFDEF,1,"VA Print Name")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM="" Q
 I '$$CK^NDFRR1(50.68,5,NDFM) D HLP^NDFRR1(50.68,5) G 10
 S ^TMP("NDFR",$J,"50.68,5")=NDFM
 K NDFDEF
 I CHANGE'=^TMP("NDFR",$J,"50.68,5")&('$D(NDFNW)) D 119
 Q:$D(NDFEDIT)
 ;
11 ;  VA PRODUCT IDENTIFIER
 N SUP,ADD K NDFDEF
 I '$D(NDFEDIT) D
 .W !,"The next available ID# is ",$$FINDPROD(0)
119 ;
 W !,"*******",!,"SUP=",$D(SUP),!,"NDFNW=",$D(NDFNW),!,"CHANGE=",$D(CHANGE)
 W !,"TMP 50.68,6 =",$G(^TMP("NDFR",$J,"50.68,6"))
 W !,"CHANGE=",$D(CHANGE),!,"*********"
 S NDFDEF=$S($D(SUP):SUP,$D(NDFNW)!($D(CHANGE)):$$FINDPROD(0),1:$S($D(^TMP("NDFR",$J,"50.68,6")):^TMP("NDFR",$J,"50.68,6"),1:"")),ADD=1
 I (NDFDEF=""!($D(CHANGE)))&('$D(NDFNW))&('$D(SUP)) W !,"The next available ID# is ",$$FINDPROD(0) S NDFDEF=$$FINDPROD(0)
 S (NDFM,X)=$$GET^NDFRR1(50.68,6,NDFDEF,1,"VA Product Identifier")
 I $D(NDFABORT)&('$D(NDFEDIT)) G ^NDFRR2
 I $D(NDFABORT)&($D(NDFEDIT)) G EXIT^NDFRR2A
 I NDFM="" Q
 I NDFM="SUP" S SUP=$$FINDPROD(1) G 119
 I $E(NDFM,2,5)["O" W !,"Number contains the letter O",$C(7) G 11
 I '$$CK^NDFRR1(50.68,6,NDFM) D HLP^NDFRR1(50.68,6) G 11
 I $$CHECKP^NDFRR3($G(^TMP("NDFR",$J,"50.68,5"))) W !,$C(7),NDFM," is assigned to another VA Product name with a different Print Name." G 119
 ; find and check identifier
 I $D(^PSNDF(50.68,"C",NDFM)) S ADD=$$PROD(NDFM) I ADD S ^TMP("NDFR",$J,"50.68,6")=NDFM
 I $G(ADD)=0 G 119
 S ^TMP("NDFR",$J,"50.68,6")=NDFM
 K NDFDEF
 Q:$D(NDFEDIT)  G ^NDFRR22
 ;
FINDPROD(SUP) ; find next available number
 N LAST,LET,II,END,SETID
 I 'SUP D
 .S LET=$S($D(NDFNW):$E(^TMP("NDFR",$J,"50.68,.01"),1,1),1:$E($P(NDFNAME,"^",2),1,1))
 .S II=LET_"0000" F  S II=$O(^PSNDF(50.68,"C",II)) Q:II=""!($E(II,1,1)'=LET)!($E(II,2)'?1N)  S LAST=II
 .I $D(LAST) S END=$E(LAST,2,5)+1 S:$L(END)=3 END="0"_END S:$L(END)=2 END="00"_END S:$L(END)=1 END="000"_END S SETID=$E(LAST,1,1)_END
 .I '$D(LAST) S:'$D(LAST) SETID=LET_"0001"
 I SUP D
 .S LET="XR"
 .S II=LET_"000" F  S II=$O(^PSNDF(50.68,"C",II)) Q:II=""!($E(II,1,2)'=LET)  S LAST=II
 .I $D(LAST) S END=$E(LAST,3,5)+1 S:$L(END)=2 END="0"_END S:$L(END)=1 END="00"_END S SETID=$E(LAST,1,2)_END
 .I '$D(LAST) S:'$D(LAST) SETID=LET_"001"
 Q SETID
 ;
PROD(NDFM) ;
 N DIR
 I +$O(^PSNDF(50.68,"C",NDFM,0))=$P($G(NDFNAME),"^") Q 1
 W !,NDFM," is already assigned to VA Product name ",$P(^PSNDF(50.68,+$O(^PSNDF(50.68,"C",NDFM,0)),0),"^")
 S DIR(0)="Y",DIR("A")="Do you want to still assign this number" D ^DIR
 Q Y
 ;
 ;
 G ^NDFRR22
