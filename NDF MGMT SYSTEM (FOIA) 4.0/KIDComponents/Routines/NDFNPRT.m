NDFNPRT ;BIR/DMA NEW REPORT ; 16 Dec 02 / 10:56 AM
 ;;4.0; NDF MANAGEMENT;; 16 Dec 02
 ;using files 5000.506 50.416 50.607 50.6 50.64 50.68
 ;
 ;NDFCNT=counter for subfiles
 ;NDFD1=hold variable for several fields contents
 ;NDFE=subfile ien, NDFE1=hold variable for several fields contents
 ;NDFFC=flag to handle blanks at start of restrictions
 ;NDFFNU=product file #, NDFG=counter, NDF1=date print hold
 ;NDFHR=$$GET1 setup variable to read subfile, NDF2=array to hold subfile
 ;NDF3=page break checker, NDF4=counter, NDFLC=page break counter
 ;NDFN=product name, NDFSI=subfile ien, NDFPG=page counter
 ;NDFPH=print heading, NDFPR=print restrictions holder
 ;NDFR=restrictions hold variable, NDFAT=audit and temp ien
 ;NDFC1=ien from audit file for product file
 ;NDFC=ien from temp file for product file, NDFSTR=strength, NDF5=counter
 ;NDF6=generic name, NDF7=dosage form, NDF8=active ingredient
 ;NDFTM=reformated restrictions, NDFUNT=units
 ;
 N NDFAT,BL,CL,NDFCNT,NDFD1,NDFE,NDFE1,NDFFC,NDFFNU,NDFG,NDF1,NDFHR,NDF2,NDF3,NDF4,NDFLC,NDFN,NDFSI,NDFPG,NDFPH,NDFPR,NDFR,NDFC1,NDFC,NDFSTR,NDF5,NDF6,NDF7,NDF8,NDFTM,NDFUNT
 S NDFFNU=50.68 K ^TMP("NDFSR",$J)
 S %H=$H D YX^%DTC S NDF1=Y,BL="" F NDFG=1:1:70 S BL=BL_"-"
 S CL=0 D ^%ZIS S:POP>0 CL=1 D:POP>0 ^%ZISC Q:CL>0
 U IO S NDFPG=1,NDFLC=0 D HEAD
F1 ;
 S NDFAT=0 F  S NDFAT=$O(^NDFK(5000.506,NDFAT)) Q:'NDFAT  S NDFC1=^(NDFAT,0),NDFN=$$GET1^DIQ(NDFFNU,NDFC1,.01),^TMP("NDFSR",$J,NDFN)=NDFC1
 S NDFAT="" F  S NDFAT=$O(^TMP("NDFSR",$J,NDFAT)) Q:NDFAT=""  S NDFC=^(NDFAT) D PRINT I NDFLC=2 W # D HEAD S NDFLC=0
 I NDFLC=1 W # S NDFLC=0
 ;
QUIT ;
 K ^TMP("NDFSR",$J)
 D ^%ZISC
 Q
 ;
PRINT ;PRINT A RECORD
 W "NAME: "_$$GET1^DIQ(NDFFNU,NDFC,.01),!,"VA GENERIC NAME: ",$$GET1^DIQ(NDFFNU,NDFC,.05),!,"DOSAGE FORM: ",$$GET1^DIQ(NDFFNU,NDFC,1),!,"VA PRINT NAME: ",$$GET1^DIQ(NDFFNU,NDFC,5),!,"VA DISPENSE UNIT: ",$$GET1^DIQ(NDFFNU,NDFC,8),!!
 ;
 S NDFSTR=$$GET1^DIQ(NDFFNU,NDFC,2),NDFUNT=$$GET1^DIQ(NDFFNU,NDFC,3) W "STRENGTH:  ",NDFSTR,?30,"UNITS: ",NDFUNT,!
 W "EXCLUDE DRUG TO DRUG INTERACTION: ",$$GET1^DIQ(NDFFNU,NDFC,23),!
 W "NATIONAL FORMULARY NAME: ",$$GET1^DIQ(NDFFNU,NDFC,4),!!
 ;
 W "VA PRODUCT IDENTIFIER: ",$$GET1^DIQ(NDFFNU,NDFC,6),?30,"TRANSMIT TO CMOP: ",$$GET1^DIQ(NDFFNU,NDFC,7),!
 S NDFD1=$$GET1^DIQ(NDFFNU,NDFC,19) W "CS SCHEDULE: ",$S(NDFD1'="":NDFD1,1:"UNKNOWN"),!
 S NDFD1=$$GET1^DIQ(NDFFNU,NDFC,20) W "MULTISOURCE/SINGLE SOURCE: ",$S(NDFD1'="":NDFD1,1:"UNKNOWN"),!!
 ;
 S NDFD1=$$GET1^DIQ(NDFFNU,NDFC,11) W "GCNSEQNO: ",$S(NDFD1'="":NDFD1,1:"UNKNOWN"),!
 S NDFD1=$$GET1^DIQ(NDFFNU,NDFC,12) W "PREVIOUS GCN SEQNO: ",$S(NDFD1'="":NDFD1,1:"UNKNOWN")
 S NDFD1=$$GET1^DIQ(NDFFNU,NDFC,13) W ?30,"NDC LINK TO GCNSEQNO: ",$S(NDFD1'="":NDFD1,1:"UNKNOWN"),!
 ;getting active ingredients(.01), strength(1) and units(2) from the
 ;active ingreadients subfile and putting in array J(
 S NDFCNT=0,NDFE=0 F  S NDFE=$O(^PSNDF(NDFFNU,NDFC,2,NDFE)) Q:'NDFE  S NDFCNT=NDFCNT+1,NDFHR=NDFE_","_NDFC_",",NDF2(NDFCNT)=$$GET1^DIQ(50.6814,NDFHR,.01)_U_$$GET1^DIQ(50.6814,NDFHR,1)_U_$$GET1^DIQ(50.6814,NDFHR,2)
 S NDF6=$$GET1^DIQ(NDFFNU,NDFC,.05)
 S NDFE1=$$GET1^DIQ(NDFFNU,NDFC,1),NDF7=$S(NDFE1'="":NDFE1,1:" UNKNOWN")
 S NDF8=$S($P($G(NDF2(1)),U)'="":$P($G(NDF2(1)),U),1:" UNKNOWN")
 W "FDB DESCRIPTION: ",NDF6_" "_NDF7_" "_NDF8,!
 ;
 F NDF4=1:1:NDFCNT Q:$P(NDF2(NDF4),U)=""  D
 .I $P(NDF2(NDF4),U,2)'=""&($P(NDF2(NDF4),U,3)'="") W "ACTIVE INGREDIENTS: ",$S($P(NDF2(NDF4),U)'="":$P(NDF2(NDF4),U),1:"UNKNOWN")
 .W !,"STRENGTH: ",$P(NDF2(NDF4),U,2),?30,"UNITS: ",$P(NDF2(NDF4),U,3),!
 I $G(^PSNDF(NDFFNU,NDFC,2,1,0))="",NDFCNT=0 W "ACTIVE INGREDIENTS: ","UNKNOWN",!,"STRENGTH: ","UNKNOWN",?30,"UNITS: ","UNKNOWN",!!
 F NDF4=1:1:NDFCNT S NDF2(NDF4)="" ;setting NDF2( array to null for next product
 ;
 S NDFE1=$$GET1^DIQ(NDFFNU,NDFC,15) W "PRIMARY VA DRUG CLASS: ",$S(NDFE1'="":NDFE1,1:"UNKNOWN")
 S NDFE1="",NDFE="",NDFE=$P($G(^PSNDF(NDFFNU,NDFC,4,0)),U,3) I NDFE S NDFE1=$$GET1^DIQ(50.6816,NDFE_","_NDFC_",",.01)
 W ?40,"SECONDARY VA CLASS: ",$S(NDFE1'="":NDFE1,1:"UNKNOWN"),!
 W "NATIONAL FORMULARY INDICATOR: ",$$GET1^DIQ(NDFFNU,NDFC,17),?40,"INACTIVE DATE: ",$$GET1^DIQ(NDFFNU,NDFC,21),!
 I $G(^PSNDF(NDFFNU,NDFC,6,1,0))="" W "NATIONAL RESTRICTIONS: ","UNKNOWN",!,BL,!! S NDFLC=NDFLC+1 D  Q
 .S NDF3=$O(^TMP("NDFSR",$J,NDFAT)) I NDF3=""&(NDFLC=2) S NDFLC=0
 ;
 ;if restrictions loop through get the information and print
 W "NATIONAL RESTRICTIONS: ",!
 S NDFSI=0 F  S NDFSI=$O(^PSNDF(NDFFNU,NDFC,6,NDFSI)) Q:NDFSI=""  S NDFR=$G(^PSNDF(NDFFNU,NDFC,6,NDFSI,0)),NDFTM="",NDFFC=0 S:$E(NDFR,1,1)=" " NDFFC=1 D
 .S:NDFFC NDFTM=$P(NDFR," ",2,$L(NDFR)) S:'NDFFC NDFTM=$P(NDFR," ",1,$L(NDFR))
 .W NDFTM,!
        W BL,!! S NDFLC=NDFLC+1 S NDF3=$O(^TMP("NDFSR",$J,NDFAT)) I NDF3=""&(NDFLC=2) S NDFLC=0
        Q
 ;
HEAD ;
 S NDFPH="VA NEW PRODUCTS REPORT" W !!,?26,NDFPH,?65,"PAGE: ",NDFPG,!
 W ?5,"DATE: ",$P(NDF1,"@"),?49,"TIME: ",$P(NDF1,"@",2),!,BL,!! S NDFPG=NDFPG+1,NDFLC=0
 Q
